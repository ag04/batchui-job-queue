plugins {
	id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

apply plugin: 'idea'

group = 'com.ag04.batchui'
version = '0.1.0'
sourceCompatibility = '11'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

repositories {
    mavenCentral()
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        if (project.hasProperty('ag04_nexus_username') && project.hasProperty('ag04_nexus_password')) {
            maven {
                url 'https://nexus.ag04.io/repository/releases'
                credentials {
                    username ag04_nexus_username
                    password ag04_nexus_password
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn publish

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'javax.validation:validation-api'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

wrapper {
    gradleVersion = "6.1"
}