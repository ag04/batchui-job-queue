plugins {
	id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

apply plugin: 'idea'

project.group = 'com.ag04.batchui'
//project.version = '1.0.5'

sourceCompatibility='11'
targetCompatibility='11'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

repositories {
    mavenCentral()
}


publishing {
    repositories {
        if (project.hasProperty('github_username') && project.hasProperty('github_password')) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ag04/batchui-job-queue")
                //url 'https://nexus.ag04.io/repository/releases'
                credentials {
                    username github_username
                    password github_password
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

afterReleaseBuild.dependsOn publish

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'javax.validation:validation-api'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

wrapper {
    gradleVersion = "6.5.1"
}